version '1.0-SNAPSHOT'

apply plugin: 'application'

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.11'
    testCompile ('io.socket:socket.io-client:0.6.3')
    compile('com.corundumstudio.socketio:netty-socketio:1.7.8')
    compile('org.postgresql:postgresql:9.4.1207.jre7')
    compile('redis.clients:jedis:2.8.0')
    compile('com.fasterxml.jackson.datatype:jackson-datatype-json-org:2.7.1')
    compile('org.json:json:20160212')
    compile project(":Annotations")
}

mainClassName = 'edu.cwru.eecs395_s16.ui.mainUI'

task processAnnotations (type: JavaCompile, group: 'build', description: 'Processes custom Annotations.') {
    source = sourceSets.main.java.srcDirs
    classpath = configurations.compile
    options.compilerArgs = [
            "-proc:only",
            "-processor","edu.cwru.eecs395_s16.annotations.NetworkEventAnnotationProcessor"
    ]
    destinationDir = project.getBuildDir()
}

task(testDebug, type: Test) {
    jvmArgs = ["-Xdebug", "-Xrunjdwp:transport=dt_socket,server=y,suspend=y,address=5005"]
    testLogging.showStandardStreams = true
    testLogging.exceptionFormat = 'full'
}

compileJava {
    options.encoding = 'UTF-8'
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
}

run {
    standardInput = System.in
    standardOutput = System.out
}

task(runDebug, dependsOn: 'classes', type: JavaExec) {
    main = mainClassName
    classpath = sourceSets.main.runtimeClasspath
    jvmArgs = ["-Xdebug", "-Xrunjdwp:transport=dt_socket,server=y,suspend=y,address=5005"]
    standardInput = System.in
}

compileJava.dependsOn processAnnotations